// Potion Settings
@setvar! drink_red 1
@setvar! drink_heal_hp 70

if maxstam > 45
    @setvar! drink_red_stam 60
else
    @setvar! drink_red_stam 14
endif

if skill 'Alchemy' >= 80
    @setvar! drink_white 1
    @setvar! drink_blue 1
    @setvar! drink_black 1
    @setvar! drink_Panacea 1
else
    @setvar! drink_white 0
    @setvar! drink_blue 0
    @setvar! drink_black 0
    @setvar! drink_Panacea 0
endif

if skill 'Healing' > 0
    @setvar! use_bandages 1
else
    @setvar! use_bandages 0
endif

@setvar! mushroom_mana 50
if skill 'Magery' > 0
    @setvar! use_shrooms 1
else
    @setvar! use_shrooms 0
endif

overhead "Now keeping you alive" 34

while not dead
    // Drink healing potion
    if hp < drink_heal_hp
        if findtype 3852 self as PotsHeal 
            getlabel PotsHeal LabelPotsHeal
            if 'next usable' in LabelPotsHeal
                sysmsg 'Unable to use Heal Potion Yet!' 33          
            else
                overhead 'Drinking heal!' 68  
                dclick PotsHeal
                wait 650  
            endif
        else
            overhead "Out of Healing Potion" 34
        endif 
    endif

    // Bandage self
    if use_bandages = 1 and hp != maxhp and bandaging = 0
        if findtype "clean bandage%s%" backpack
            hotkey 'Bandage Self' 
            wait 650
        else
            overhead "Out of Bandages" 34
        endif
    endif 

    // Explosion Pouch
    if paralyzed
        overhead 'Paralyzed!' 38
        if findtype "pouch" backpack 38 any any 
            say '[pouch'
            wait 650
        else
            overhead "Out of Trap Pouch" 34
        endif 
    endif
    
    // Drink cure potion for bleed/disease
    if drink_Panacea = 1 and cooldown "Pancea" = 0
        if findbuff "bleed" or findbuff "disease"
            if findtype "Orange Potion" self
                wait 650
                overhead 'Drinking Panacea!' 48
                potion "cure"
                wait 650
            else
            overhead "Out of Cure Potion" 34
        endif 
        endif
    endif

    // Drink cure potion for poison
    if poisoned
        if findtype "Orange Potion" self
            wait 650            
            overhead 'Drinking cure!' 48
            potion "cure" 
            wait 650      
        else
            overhead "Out of Cure Potion" 34
        endif 
    endif
    
    // Drink refresh potion
    if drink_red = 1 and stam <  drink_red_stam
        if findtype "Red Potion" self as pot
            dclick pot
            wait 200
        else
            overhead "Out of Refresh Potion" 34
        endif
    endif

    // Buffs
    // Strength
    if drink_white = 1
        if cooldown "Strength Potion" < 2000 or not findbuff "Strength"
            if findtype "White Potion" self as pot
                dclick pot
                wait 200
            else
                overhead "Out of Strength Potion" 34
            endif
        endif
    endif
    // Agility
    if drink_blue = 1
        if cooldown "Agility Potion" < 2000 or not findbuff "Agility"
            if findtype "Blue Potion" self as pot
                dclick pot
                wait 200
            else
                overhead "Out of Agility Potion" 34
            endif
        endif
    endif
    // Magic Resist
    if drink_black = 1
        if cooldown "Magic Resist Potion" < 2000 or not findbuff "Magic Resist Potion"
            if findtype "Black Potion" self as pot
                dclick pot
                wait 200
            else
                overhead "Out of Magic Resist Potion" 34
            endif
        endif
    endif

    if use_shrooms = 1 
        if not findbuff "Magic Reflection"
            overhead "Magic Reflection down" 34
            wait 200
        endif

        if not findbuff "Reactive Armor"
            overhead "Reactive Armor down" 34
            wait 200
        endif
    endif

    // Food satisfaction
    if findtype "tray" backpack as food
        if not findbuff "food satisfaction"
            overhead 'Eating Food!' 68  
            dclick food
            wait 650
        endif
    endif

    // Magic Mushroom
    if use_shrooms = 1
        if mana < mushroom_mana and not findbuff "Actively Meditating"
            if findtype 29012 backpack as shroom
                if cooldown mushroom < 1
                    dclick shroom
                    overhead 'Eating Shroom!' 68
                    wait 650
                else
                    sysmsg 'Unable to use Shroom Yet!' 33
                endif
            else
                overhead "Out of Shrooms" 34
            endif
        endif
    endif

   pause 250
endwhile 